buildscript {
	ext {
		springBootVersion = '2.0.0.RC2'
	}
	repositories {
		maven {
			url 'https://repo.spring.io/libs-milestone'
		}
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:+'
		classpath 'com.netflix.nebula:nebula-project-plugin:+'
		classpath 'com.netflix.nebula:nebula-release-plugin:+'
		classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:+'
	}
}

allprojects {
	group = 'info.developerblog.spring.cloud'
	version = '0.0.1'

	apply plugin: 'idea'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'nebula.nebula-release'

	repositories {
		mavenLocal()
		maven {
			url 'https://repo.spring.io/libs-milestone'
		}
		jcenter()
	}

	dependencyManagement {
		imports {
			mavenBom 'org.springframework.cloud:spring-cloud-starter-parent:Finchley.M7'
		}

		dependencies {
			dependency 'ru.odnoklassniki:one-nio:1.0.2'
			dependency 'org.projectlombok:lombok:1.16.8'
		}
	}

	idea {
		module {
			outputDir file('build/classes/main')
			testOutputDir file('build/classes/test')
		}
	}
}

subprojects {
	apply plugin: 'java'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	test {
		reports {
			junitXml.enabled = true
			html.enabled = true
		}
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'

		testCompile('org.springframework.boot:spring-boot-starter-test')
	}
}